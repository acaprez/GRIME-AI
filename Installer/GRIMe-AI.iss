; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "GRIME-AI"
#define MyAppVersion "1_0_0_0 (Official)"
#define MyAppPublisher "Blade Vision Systems"
#define MyAppURL "https://www.BladeVisionSystems.com"
#define MyAppExeName "GRIME-AI.exe"
#define MyAppIcoName "GRIME-AI Logo.ico"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{617A21D5-2E34-4C6D-9824-3744D49278F9}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DisableDirPage=yes
DisableProgramGroupPage=yes
DiskSpanning=yes

; Enable logging by default (no /LOG switch required)
SetupLogging=yes

; Replacing periods in version number with underscores because for some reason, Teams doesn't like all
; the periods.
;Password=C0rnHusk3r%

; Uncomment the following line to run in non administrative install mode (install for current user only.)
; PrivilegesRequired=lowest
OutputDir=..\Installer
OutputBaseFilename=GRIME-AI 1_0_0_0 (Official) Setup
Compression=lzma2
SolidCompression=yes
WizardStyle=modern
ChangesEnvironment=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "french";  MessagesFile: "compiler:Languages\French.isl"
Name: "german";  MessagesFile: "compiler:Languages\German.isl"
Name: "italian"; MessagesFile: "compiler:Languages\Italian.isl"
Name: "spanish"; MessagesFile: "compiler:Languages\Spanish.isl"

[Tasks]
; --- Existing task ---
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
; --- NEW TASK: Optional Install of R ---
Name: "installR"; Description: "Install R"; GroupDescription: "Optional Components:"; Flags: unchecked

[Dirs]
; Create models folder in the user's Documents directory
Name: "{app}\models"
; Create configuration folder in the user's Documents directory
Name: "{userdocs}\GRIME-AI\Settings"; Flags: uninsalwaysuninstall

[Files]
; GRIME-AI application
Source: "..\dist\main\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs
;
; 3rd Party Installers
; installR so the R installer is only installed when selected ---
Source: "..\R-4.4.1-win.exe"; DestDir: "{app}"; AfterInstall: RunOtherInstaller; Flags: ignoreversion; Tasks: installR
;
; machine learning files
Source: "..\sam2\*"; DestDir: "{app}\sam2"; Flags: recursesubdirs createallsubdirs
Source: "..\patches\transforms.py"; DestDir: "{app}\sam2\sam2\utils"

;PROVISIONAL CONTINGENT 
;Source: "..\sam2\sam2\configs"; DestDir: "{app}\sam2\sam2\configs"; Flags: recursesubdirs createallsubdirs
;Source: "..\sam2\checkpoints"; DestDir: "{app}\sam2\checkpoints"; Flags: recursesubdirs createallsubdirs
;Source: "..\venv\Lib\site-packages\sam2\*"; DestDir: "{app}\sam2"; Flags: recursesubdirs createallsubdirs
;
; Copy
; Save the site_config.json to the user's Documents folder
Source: "..\site_config.json"; DestDir: "{userdocs}\GRIME-AI\Settings"; Flags: ignoreversion
;
; user interface files
Source: "..\src\GRIME_AI\icons\*.*"; DestDir: "{app}\icons"
Source: "..\Installer\ui\QDialog*.ui"; DestDir: "{app}"
;
; documentation
Source: "..\Documentation\GRIME-AI_v0059_v2.pdf"; DestDir: "{app}\Documentation"
;
; miscellaneous
; Source: "C:\Users\Astrid Haugen\PycharmProjects\neonAI\chromedriver.exe"; DestDir: "{app}\chromedriver"

; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; \
    IconFilename: "{app}\{#MyAppIcoName}"; Tasks: desktopicon

[Registry]
Root: HKCU; Subkey: "Environment"; ValueType: string; ValueName: "R_HOME"; ValueData: "C:\Program Files\R\R-4.4.1"; Flags: preservestringtype

[Run]
; --- Conditioned to run only if the installR task is selected ---
Filename: "{commonpf64}\R\R-4.4.1\bin\x64\r.exe"; Parameters: "-e ""install.packages('neonUtilities', repos='https://cran.rstudio.com/')"""; Tasks: installR

; --- Launch the main application ---
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

; --- Also run pip install command ---
Filename: "{cmd}"; Parameters: "/C pip install torch torchvision torchaudio --index-url https://download.pytorch.org/whl/cu126"; WorkingDir: "{app}"

[Code]
procedure RunOtherInstaller;
var
  ResultCode: Integer;
begin
  if not Exec(ExpandConstant('{app}\R-4.4.1-win.exe'), '', '', SW_SHOWNORMAL,
    ewWaitUntilTerminated, ResultCode)
  then
    MsgBox('Other installer failed to run!' + #13#10 +
      SysErrorMessage(ResultCode), mbError, MB_OK);
end;

// Override default log filename so every run drops a versioned log next to the app
function GetLogFileName(var FileName: String): Boolean;
begin
  FileName := ExpandConstant( '{app}\Installer Log - {#StringChange(MyAppVersion, '.', '_')}.log' );
  Result := True;
end;
